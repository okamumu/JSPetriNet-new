/*
  SPNP Example 10: Birth-death Model for the BUS in ATM LAN emulation
  (H. Sun, X. Zang and K.S. Trivedi,
   Performance of broadcast and unknown server (BUS) in ATM LAN emulation,
   Technical Report, Duke University, 1999)
*/

PLST = 2   // Short packet size
PLMD = 4   // Medium packet size
PLLG = 32  // Long packet size

c1 = 0.342
c2 = 0.093
c3 = 0.565

rateOUT = 5000

K = 3
lambda = 0.9
mu = 1
beta = 1.1

BN = 40  // Buffer size
// QLEN = 0

g0 = 0.01
g1 = 0.05
beta_01 = 0.5
beta_10 = 0.5
a0 = 0.01
a1 = 0.05
alpha_01 = 0.5
alpha_10 = 0.5

place PAR
place PCS1
place PCS2

place PARV
place PST
place PMD
place PLG
place PBUF
place POUT
place Pbroad

place Puon
place Puoff
place Pdepart

exp TARV1 (rate = lambda, guard = guardARV)
exp TAR (rate = mu)
exp TCS (rate = beta)
imm tCS (priority = 20, weight = 1)

exp Tbroad (rate = rateTbroad)
exp Yon (rate = beta_01)
exp Yoff (rate = beta_10)
exp Tuon (rate = alpha_01 * #Puoff)
exp Tuoff (rate = alpha_10 * #Puon)
exp Tunicast (rate = rateTunicast, guard = guardTunicast)
imm t1 (priority = 20, weight = 1)
imm t2 (priority = 20, weight = 1, guard = guardt2)

imm tSTI (priority = 20, weight = c1)
imm tMDI (priority = 20, weight = c2)
imm tLGI (priority = 20, weight = c3)
imm tSTO (priority = 40, weight = 1, guard = guardSTO)
imm tMDO (priority = 40, weight = 1, guard = guardMDO)
imm tLGO (priority = 40, weight = 1, guard = guardLGO)
imm tSTL (priority = 20, weight = 1)
imm tMDL (priority = 20, weight = 1)
imm tLGL (priority = 20, weight = 1)
imm tOUT (priority = 20, weight = 1)
exp TOUT (rate = rateOUT)

oarc TARV1 to PAR
iarc PAR to TAR
oarc TAR to PCS1
iarc PCS1 to tCS
oarc tCS to PCS2 (multi = 3)
iarc PCS2 to TCS
harc PCS2 to tCS

oarc Yon to Pbroad
iarc Pbroad to Yoff
harc Pbroad to Yon

oarc Tbroad to PARV

oarc TARV1 to Puon
oarc TCS to Pdepart
oarc Tuon to Puon
iarc Puon to Tuoff
iarc Puon to t1
iarc Pdepart to t1 (multi = 3)
iarc Puoff to t2
iarc Pdepart to t2 (multi = 3)
oarc Tuoff to Puoff
iarc Puoff to Tuon

oarc Tunicast to PARV

iarc PARV to tSTI
iarc PARV to tMDI
iarc PARV to tLGI
oarc tSTI to PST
oarc tMDI to PMD
oarc tLGI to PLG
iarc PST to tSTO
iarc PST to tSTL
iarc PMD to tMDO
iarc PMD to tMDL
iarc PLG to tLGO
iarc PLG to tLGL
oarc tSTO to PBUF (multi = PLST)
oarc tLGO to PBUF (multi = PLLG)
oarc tMDO to PBUF (multi = PLMD)
iarc PBUF to TOUT
oarc tOUT to POUT (multi = 3)
iarc POUT to TOUT
harc POUT to tOUT

// REWARD Functions
QL = ifelse(#PBUF > QLEN, 1, 0)
MLP = ifelse(#PBUF > BN - PLMD, 1, 0)
SLP = ifelse(#PBUF > BN - PLST, 1, 0)
LLP = ifelse(#PBUF > BN - PLLG, 1, 0)

// GUARD Functions
n = ifelse(#PCS2 > 0, 1, 0)
guardARV = #PAR + #PCS1 + n < K
guardt2 = #Puon == 0
guardTunicast = #Puon > 0
guardSTO = #PBUF <= BN - PLST
guardMDO = #PBUF <= BN - PLMD
guardLGO = #PBUF <= BN - PLLG

// RATE Functions
rateTbroad = ifelse(#Pbroad > 0, g1, g0)
rateTunicast = #Puon * a1 + #Puoff * a0
