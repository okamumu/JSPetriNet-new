/*
  Example: RAID10
  F. Machida, R. Xia and K.S. Trivedi,
  Performability modeling for RAID storage systems by Markov regenerative process,
  IEEE Transactions on Dependable and Secure Computing
*/

// HDD model

place Pn (init = 6)
place Pdf
place Pf

exp Tfail (guard = gdfail, rate = Tfail.rate)
exp Tdfail (guard = gdfail, rate = Tdfail.rate)
gen Trebuild (guard = gdfail, dist = Trebuild.dist)
imm Tinitf1 (guard = ginitf)
imm Tinitf2 (guard = ginitf)

arc Pn to Tdfail
arc Pn to Tfail
arc Tdfail to Pdf
arc Tfail to Pf
arc Pdf to Trebuild
arc Pdf to Tinitf1
arc Pf to Tinitf2
arc Trebuild to Pn
arc Tinitf1 to Pn
arc Tinitf2 to Pn

// Reconstruction model

place Po (init = 1)
place Pr
place Pc

gen Trecon
imm Tstart (guard = gstart)
imm Tinitr (guard = ginitr)

arc Po to Tstart
arc Tstart to Pr
arc Pr to Trecon
arc Trecon to Pc
arc Pc to Tinitr
arc Tinitr to Po

// rates and guards

Tdfail.rate = #Pn * lambda * (1 - (#Pdf / (#Pdf + #Pn)))
Tfail.rate = #Pn * lambda * (#Pdf / (#Pdf + #Pn))

gdfail = #Po == 1
gstart = #Pdf > 3 || #Pf == 1
ginitf = #Pc == 1
ginitr = #Pdf == 0 && #Pf == 0

// params

Trebuild.dist = det(MTTR1)
Trecon.dist = det(MTTR2)

MTTF = 1.0e+6 // [hours]
lambda = 1/MTTF
MTTR1 = 2 // [hours]
MTTR2 = 24 // [hours]
